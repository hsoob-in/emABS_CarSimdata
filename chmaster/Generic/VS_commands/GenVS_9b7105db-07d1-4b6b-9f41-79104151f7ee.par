PARSFILE
#FullDataName Generic VS Commands`FCW Alert and AEB Control`Safety Assist Euro NCAP
#VehCode AEB, FCW Euro NCAP

#MiscYellow0
*************** AEB Tuning Parameters (refined for speeds from 10-60 kph) *************** 
DEFINE_PARAMETER TTC_THRESH_FCW = 3.0; s; TTC threshold for the FCW Alert
DEFINE_PARAMETER TTC_BRAKE_THRESH_TIME = 1.35; s; TTC threshold for the AEB application
DEFINE_PARAMETER AEB_THRESH_BRAKE_DIST = 7.25; m; Long. distance threshold
DEFINE_PARAMETER PED_LAT_THRESH = 2.2; m ; lateral distance threshold

DEFINE_PARAMETER MIN_THRESH = 0.05; - ; Minimum threshold for x-distance between ...
the pedestrian and the vehicle = value just greater than 0
DEFINE_PARAMETER AEB_MAX_BK_REQ = 5; MPa; AEB max brake pressure request
DEFINE_PARAMETER AEB_MAX_REL_SPD = 100; km/h; max allowable relative speed calc.
DEFINE_PARAMETER AEB_Min_Stop_Speed = 5; km/h; threshold speed to cause a complete stop
******************************************************************************************      

DEFINE_PARAMETER AEB_TimeThresh = 0; s ;  apply brakes parameter
DEFINE_PARAMETER AEB_DistThresh = 0; m ;  apply brakes parameter

DEFINE_OUTPUT Long_Ped_Rel = if( MagS<<s>>_1,    (DisS<<s>>_1*(cos(BrCs<<s>>_1)) -  ...
LxBumper), 0) ; m; relative longitudinal distance between the pedestrian target ...
and the camera sensor.  The front bumper offset must be subtracted after calculating the raw...
value from the sensor output

DEFINE_OUTPUT Lat_Ped_Rel = if(MagS<<s>>_1,  DisS<<s>>_1*(sin (BrCs<<s>>_1) ) , 0) ; m; relative lateral...
 distance between the pedestrian target and the camera sensor

DEFINE_OUTPUT RelBearingBumper = if(abs(Lat_Ped_Rel) >MIN_THRESH,...
 atan(Lat_Ped_Rel/(Long_Ped_Rel) ), 0) 

DEFINE_OUTPUT RelDistFrontBump = if(abs(RelBearingBumper) > 0, ...
Lat_Ped_Rel/(sin(RelBearingBumper)), Long_Ped_Rel); m; ...
Relative distance from center of the front bumper to the target object detected

!Saturation for relative speed, after the instance a realtive distance value goes from -1 to 
! a value greater than zero in the smallest time interval
DEFINE_OUTPUT RelSpeedOk = if( abs(SpdS<<s>>_1) < AEB_MAX_REL_SPD , SpdS<<s>>_1, 0); -; boolean

! Time to collision is the relative distance / relative speed between the ego vehicle and 
! the closest detected object
DEFINE_OUTPUT TTC_AEB =   if(MagS<<s>>_1 & (RelSpeedOk*3.6 < -MIN_THRESH), ...
abs(RelDistFrontBump/ SpdS<<s>>_1) , 0); s; time to collision

DEFINE_OUTPUT AEB_On = 1; - ;  Boolean: switch to activate system AEB
DEFINE_OUTPUT Show_FCW = 0; - ;  Boolean: apply FCW
DEFINE_OUTPUT ApplyAEB_FullStop = 0; - ;  Boolean: apply brakes

EQ_OUT AEB_TimeThresh =   TTC_AEB < TTC_BRAKE_THRESH_TIME & TTC_AEB > MIN_THRESH 
EQ_OUT AEB_DistThresh =   MagS<<s>>_1 & Long_Ped_Rel < AEB_THRESH_BRAKE_DIST ... 
& Lat_Ped_Rel < PED_LAT_THRESH; - ; boolean

!FCW Warning Output
EQ_OUT Show_FCW =  (MagS<<s>>_1 & RelSpeedOk < 0 & TTC_AEB < TTC_THRESH_FCW) | ...
ApplyAEB_FullStop

!AEB Output
EQ_OUT ApplyAEB_FullStop =  if(AEB_On & MagS<<s>>_1 ,  AEB_TimeThresh  | AEB_DistThresh, 0); - ; if the AEB ...
system is engaged, determine if conditions are necessary to apply the brakes

! When AEB is requested, replace master cylinder pressure with AEB requested pressure
PBK_CON_EQUATION AEB_MAX_BK_REQ*ApplyAEB_FullStop

EQ_OUT OPT_SC = if(ApplyAEB_FullStop, 0, 3) ! speed control is 0 or 3
#ENDMYellow

*MODELCODE AEB, FCW Euro NCAP
PARSFILE Animator\HUD\HUD_ba3324b0-fcb9-45f8-abcd-5ab3d2d1a3cd.par
#BlueLink0 Animator: HUD`FCW Alert (small - right side)` Euro NCAP` , Miscellaneous`HUD_ba3324b0-fcb9-45f8-abcd-5ab3d2d1a3cd


#EMBEDDED_NOTES
#Logic to generate a forward collision warning (FCW) using the current sensor <<s>> and a parameter for the acceptable deceleration.
#
#An equation is included for open-loop brake pressure via the configurable function PBK_CON.
#
#
#END_EMBEDDED_NOTES

LOG_ENTRY Used Dataset: Generic VS Commands; { Safety Assist Euro NCAP } FCW Alert and AEB Control
#Library : Generic VS Commands
#DataSet : FCW Alert and AEB Control
#Category: Safety Assist Euro NCAP
#FileID  : GenVS_9b7105db-07d1-4b6b-9f41-79104151f7ee
#Product : CarSim 2021.0
#VehCode AEB, FCW Euro NCAP

END
